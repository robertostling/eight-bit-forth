VARIABLEW KERNAL-ROUTINE
: KERNAL-CALL [ KERNAL-ROUTINE JMPI ] ;
: KERNAL ( A X Y C W:ADR -- A X Y C )
  KERNAL-ROUTINE !W [
  >A LSR IMPL
  TXA PHA
  3 S>A PHA  TOS>A TAY  NOS>A TAX  PLA
  ] KERNAL-CALL [
  A>T  1 T+ STXZP
  PLA TAX
  TYA A>TOS  1 T+>A A>NOS  T>A 3 A>S
  0 LDA IMM  ROL IMPL  A>
  ] ;
: KERNAL-OPEN ( -- )
  0 0 0 0  FF C0 KERNAL  DROPW DROPW ;
: KERNAL-CLOSE ( LOG -- )
  0 0 0  FF C3 KERNAL  DROPW DROPW ;
: KERNAL-SETLFS ( FILENO DEV SEC -- )
  0  FF BA KERNAL DROPW DROPW ;
: KERNAL-SETNAM ( W:STR -- )
  COUNT -ROT SWAP 0  FF BD KERNAL
  DROPW DROPW ;
: KERNAL-LOAD
  ( W:START -- W:LAST/ERRNO ERRF )
  0 -ROT SWAP 0  FF D5 KERNAL 
  IF DROPW 1
  ELSE ROT DROP SWAP 0 THEN ;
: KERNAL-GETIN ( -- BYTE )
  0 0 0 0  FF E4 KERNAL  DROPW DROP ;
: KERNAL-SAVE
  ( W:START W:LAST -- ERRNO ERRF )
  SWAP  SWAPW [
  >A ZSAVE STA ZP
  >A ZSAVE 1+ STA ZP
  ] ZSAVE -ROT  0
  FF D8 KERNAL  NIP NIP ;
: KERNAL-KEY
  KERNAL-GETIN ?DUP SKIP KERNAL-KEY ;
: KEY MAKER KERNAL-KEY ;
20 ARRAY FILENAME-BUFFER
: PREPEND-SCRATCH ( W:NAME -- )
  DUPW @ DUP 2 +  0 FILENAME-BUFFER !
  FOR
    DUPW 0 I +W @
    I 2 + FILENAME-BUFFER !
  NEXT DROPW
  [CHAR] S 1 FILENAME-BUFFER !
  [CHAR] : 2 FILENAME-BUFFER ! ;
: SCRATCH-FILE ( DEV W:NAME -- )
  PREPEND-SCRATCH
  0 FILENAME-BUFFER KERNAL-SETNAM
  F SWAP F KERNAL-SETLFS
  KERNAL-OPEN
  F KERNAL-CLOSE ;
: JIFFY@W 0 A1 @W SWAP ;
: JIFFYS 0 SWAP JIFFY@W +W
  BEGIN JIFFY@W OVERW >=W UNTIL
  DROPW ;
: SECONDS FOR 3C JIFFYS NEXT ;
: CURSOR@ ( -- ROW COL )
  0 0 0 1  FF F0 KERNAL
  DROP  ROT DROP ;
: CURSOR! ( ROW COL -- )
  0 -ROT 0  FF F0 KERNAL
  DROPW DROPW ;
