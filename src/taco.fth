( SIMPLIFIED VERSION OF "TACO CAT GOAT
  CHEESE PIZZA" )
: JOYSTICK ( PLAYER -- X )
  DC SWAP 1- @ ;
VARIABLEW TIME0
: CURRENT-TIME 0 A1 @W SWAP ;
: START-TIMER CURRENT-TIME TIME0 !W ;
: TIMEOUT? CURRENT-TIME TIME0 @W -W 0 C0 >=W ;
2 ARRAY PLAYER-CARDS
: PRESSED? AND 0= ;
10 VAR FIRE
8 VAR RIGHT
4 VAR LEFT
2 VAR DOWN
1 VAR UP
VARIABLE SLAPPED
: REACT
  0 SLAPPED !
  START-TIMER
  BEGIN
    TIMEOUT? SKIP
    1 JOYSTICK FIRE PRESSED?
    IF  1 SLAPPED !  EXIT  THEN
    2 JOYSTICK FIRE PRESSED?
    IF  2 SLAPPED !  EXIT  THEN
  AGAIN ;
5 VAR #CARD-NAMES
#CARD-NAMES ARRAYW CARD-NAMES
S" TACO"   0 CARD-NAMES !W
S" CAT"    1 CARD-NAMES !W
S" GOAT"   2 CARD-NAMES !W
S" CHEESE" 3 CARD-NAMES !W
S" PIZZA"  4 CARD-NAMES !W
VARIABLE CARDS-IN-PILE
VARIABLE NEXT-CALL
VARIABLE SHOWN-CARD
VARIABLE NEXT-PLAYER
: SHOULD-SLAP? NEXT-CALL @ SHOWN-CARD @ = ;
( FIX BELOW, MOD 5 NOT MOD 4 )
: RANDOM-CARD 0 A2 @  3 AND ;
: OTHER-PLAYER 3 XOR ;
: NO-SLAP
  NEXT-CALL @ 1+
  DUP #CARD-NAMES = IF
    DROP 0
  THEN NEXT-CALL ! ;
: BAR 12 EMIT FOR E0 EMIT NEXT 12 EMIT ;
: .CARD CARD-NAMES @W COUNT TYPE ;
: EMITS ( CHAR N -- )
  FOR DUP EMIT NEXT DROP ;
: DISPLAY
  93 EMIT
  ." PLAYER 1: " 0 PLAYER-CARDS @ BAR CR
  ." PLAYER 2: " 1 PLAYER-CARDS @ BAR CR
  ." PILE:     " CARDS-IN-PILE @ BAR CR
  ." CALL:     " NEXT-CALL @ .CARD 21 EMIT
  B 10 CURSOR!
  D5 EMIT  63 8 EMITS  C9 EMIT
  C 10 CURSOR! 62 EMIT SPACE
  SHOWN-CARD @ .CARD
  C 19 CURSOR! 62 EMIT
  D 10 CURSOR!
  CA EMIT  63 8 EMITS  CB EMIT ;
: START-OVER
  1 CARDS-IN-PILE !
  0 NEXT-CALL !
  1 NEXT-PLAYER ! ;
: WINNER
  0 PLAYER-CARDS @ 0= IF  1 EXIT  THEN
  1 PLAYER-CARDS @ 0= IF  2 EXIT  THEN
  0 ;
: ROUND
  FF NEXT-PLAYER @ 1- PLAYER-CARDS +!
  RANDOM-CARD SHOWN-CARD !
  DISPLAY
  REACT
  SLAPPED @ ?DUP IF
    SHOULD-SLAP? IF OTHER-PLAYER THEN
    DUP NEXT-PLAYER !
    10 A CURSOR!
    ." PLAYER " DUP . ." GETS "
    CARDS-IN-PILE @ . ." CARDS"
    1- PLAYER-CARDS CARDS-IN-PILE @ -ROT +!
    START-OVER
    3 SECONDS
  ELSE
    NO-SLAP
    1 CARDS-IN-PILE +!
    NEXT-PLAYER @
    OTHER-PLAYER NEXT-PLAYER !
  THEN ;
: INITIALIZE-GAME
  8 0 PLAYER-CARDS !  8 1 PLAYER-CARDS !
  START-OVER ;
: HAVE-A-WINNER ( PLAYER -- )
  93 EMIT  C F CURSOR!
  ." PLAYER " . ." WON!"
  16 4 CURSOR!
  ." PRESS STOP TO LEAVE,
    ANY OTHER KEY TO RESTART"
  KEY 3 = ;
: PLAY
  INITIALIZE-GAME
  BEGIN
    ROUND
    WINNER ?DUP IF
      HAVE-A-WINNER SO
      INITIALIZE-GAME
    THEN
  AGAIN ;
PLAY
