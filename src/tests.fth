.( TESTING UTILITIES )
: .CONTEXT
  0 ZINPUT @W 0 40 -W 40 TYPE
  ." ****"
  0 ZINPUT @W 40 TYPE ;
: ASSERT ( F -- ) SKIP .CONTEXT
  CR ." ASSERTION FAILED!"
  STATUS ;
( ------------------------------------ )
.( TESTING MEMORY MANIPULATION )
73 STACK> 10 !
88 STACK> 11 !
STACK> 10 @  73 = ASSERT
STACK> 11 @  88 = ASSERT
STACK> 10 @W  73 = ASSERT 88 = ASSERT
AB BA STACK> 10 !W
STACK> 10 @W  BA = ASSERT AB = ASSERT
11 22 STACK> 10 +!W
STACK> 10 @W  DC = ASSERT BC = ASSERT
11 STACK> 10 +!
STACK> 10 @  ED = ASSERT
STATUS
( ------------------------------------ )
.( TESTING ARITHMETIC )
( ' -W 10 DUMP CR )
84 11 -  73 = ASSERT
62 11 +  73 = ASSERT
33 61 OR  73 = ASSERT
77 F3 AND  73 = ASSERT
72 1+  73 = ASSERT
74 1-  73 = ASSERT
9A 98  11 22 +W BA = ASSERT AB = ASSERT
BC DC  11 22 -W BA = ASSERT AB = ASSERT
STATUS
( ------------------------------------ )
.( TESTING COMPARISON )
3 7 < ASSERT
7 3 < 0= ASSERT
7 3 > ASSERT
3 7 > 0= ASSERT
73 37 <> ASSERT
7 7 >= ASSERT
7 7 <= ASSERT
8 7 <= 0= ASSERT
7 8 >= 0= ASSERT
6 7 <= ASSERT
7 6 >= ASSERT
AB BA AB BA =W ASSERT
AB BA BA AB =W 0= ASSERT
AB BA BA AB <>W ASSERT
STATUS
( ------------------------------------ )
.( TESTING COMPILER )
: TEST-IMMEDIATE1 73 ; IMMEDIATE
: TEST-IMMEDIATE2 37 TEST-IMMEDIATE1 ;
TEST-IMMEDIATE2 37 = ASSERT 73 = ASSERT
STATUS
( ------------------------------------ )
.( TESTING RETURN STACK/LITERALS )
: TEST-R>W  AB BA >RW 73 R>W ;
TEST-R>W  AB BA =W ASSERT 73 = ASSERT
: TEST-LIT  LIT [ CD , ] ;
DC TEST-LIT CD = ASSERT DC = ASSERT
: TEST-LITW  LITW [ 65 02 ,2 ] ;
13 37 TEST-LITW
65 02 =W ASSERT 13 37 =W ASSERT
STATUS
( ------------------------------------ )
.( TESTING STRINGS )
CHAR 0  30 = ASSERT
CHAR  1  1 = ASSERT 20 = ASSERT
73
S" HELLO THERE?" COUNT TYPE CR
73 = ASSERT
STATUS
( ------------------------------------ )
.( TESTING CONDITIONALS )
: TEST3 IF 73 ELSE 37 THEN ;
TRUE TEST3  73 = ASSERT
FALSE TEST3  37 = ASSERT
: TEST4 IF 73 EXIT THEN 37 ;
TRUE TEST4  73 = ASSERT
FALSE TEST4  37 = ASSERT
: TEST5 AB BA DROP ;
TEST5  AB = ASSERT
' TEST5 EXECUTE  AB = ASSERT
STATUS
( ------------------------------------ )
.( TESTING LOOPS )
: LOG4 0
  BEGIN
    SWAP 2/ 2/ SWAP 1+
    OVER 1 <= IF NIP EXIT THEN
  AGAIN ;
13 LOG4  2 = ASSERT
: LOG2 0
  BEGIN
    SWAP 2/ SWAP 1+
  OVER 1 <= UNTIL
  NIP ;
13 LOG2  4 = ASSERT
: TEST-FOR 0 SWAP FOR I + NEXT ;
73 5 TEST-FOR  0F = ASSERT  73 = ASSERT
: TEST-DO 0  6 2 DO  I +  1 +LOOP ;
37 TEST-DO 0E = ASSERT 37 = ASSERT
STATUS
( ------------------------------------ )
.( TESTING POSTPONE )
: TEST-POSTPONE1 POSTPONE + ; IMMEDIATE
: TEST-POSTPONE2 64 0F TEST-POSTPONE1 ;
TEST-POSTPONE2  73 = ASSERT
( TODO: POSTPONE WITH IMMEDIATE WORD )
STATUS
( ------------------------------------ )
.( TESTING VARIABLES )
VARIABLE TEST-VARIABLE-X
VARIABLE TEST-VARIABLE-Y
TEST-VARIABLE-X @ 0= ASSERT
TEST-VARIABLE-Y @ 0= ASSERT
17 TEST-VARIABLE-X !
20 TEST-VARIABLE-Y !
TEST-VARIABLE-X @ TEST-VARIABLE-Y @ +
37 = ASSERT
STATUS
( ------------------------------------ )
.( TESTING LITERALS/CONSTANTS )
: TEST-LITERALW [ 12 34 ] LITERALW ;
56 TEST-LITERALW
12 34 =W ASSERT  56 = ASSERT
73 CONSTANT TEST-CONSTANT
37 TEST-CONSTANT
73 = ASSERT 37 = ASSERT
STATUS
( ------------------------------------ )


