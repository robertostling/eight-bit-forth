( MORSE CODE )
VARIABLE DIT-LENGTH
4 DIT-LENGTH !
: BEEP ( LEN -- )
  TRIANGLE GATE OR VOICE1 CONTROL
  JIFFYS
  TRIANGLE VOICE1 CONTROL ;
: 3* DUP 2* + ;
: DIT DIT-LENGTH @ BEEP
  DIT-LENGTH @ JIFFYS ;
: DAH DIT-LENGTH @ 3* BEEP
  DIT-LENGTH @ JIFFYS ;
: LETTER-SPACING
  DIT-LENGTH @ 2* JIFFYS ;
: WORD-SPACING
  DIT-LENGTH @ 2* 2* JIFFYS ;
: CW-CHAR ( X N -- )
  FOR
    DUP 0< IF DAH ELSE DIT THEN
  2* NEXT DROP ;
CREATE MORSE-TABLE
6 CC ,2 ( , )
0 0  ,2
6 54 ,2 ( . )
0 0  ,2
5 F8 ,2 ( 0 )
5 78 ,2
5 38 ,2
5 18 ,2
5 8  ,2
5 0  ,2
5 80 ,2
5 C0 ,2
5 E0 ,2
5 F0 ,2 ( 9 )
0 0  ,2
0 0  ,2
0 0  ,2
5 88 ,2 ( = )
0 0  ,2
6 30 ,2 ( ? )
0 0  ,2
2 40 ,2 ( A )
4 80 ,2
4 A0 ,2
3 80 ,2
1 0  ,2
4 20 ,2 ( F )
3 C0 ,2
4 0  ,2
2 0  ,2
4 70 ,2
3 A0 ,2 ( K )
4 40 ,2
2 C0 ,2
2 80 ,2
3 E0 ,2
4 60 ,2 ( P )
4 D0 ,2
3 40 ,2
3 0  ,2
1 80 ,2
3 20 ,2 ( U )
4 10 ,2
3 60 ,2
4 90 ,2
4 B0 ,2
4 C0 ,2 ( Z )
: CW-EMIT ( C -- )
  DUP 20 = IF
    DROP WORD-SPACING EXIT
  THEN
  DUP [CHAR] , <  OVER [CHAR] Z >  OR
  IF DROP EXIT THEN
  [CHAR] , - 2*  0 SWAP MORSE-TABLE +W
  @W SWAP
  DUP 0= IF DROPW EXIT THEN
  CW-CHAR LETTER-SPACING ;
: BPF
  BAND-PASS FILTERS
  7 FILTER-CONTROL
  8 00 FILTER-FREQUENCY ;
: ENABLE-NOISE
  20 00 VOICE3 FREQUENCY
  1 1 VOICE3 DECAY-ATTACK
  0 F VOICE3 RELEASE-SUSTAIN
  NOISE GATE OR VOICE3 CONTROL ;
: TURN-UP ( SPEED -- )
  F 0 DO I VOLUME DUP JIFFYS 1 +LOOP
  DROP ;
: TURN-DOWN ( SPEED -- )
  0 10 DO
    I 1- VOLUME DUP JIFFYS
  FF +LOOP DROP ;
: ENABLE-MORSE
  0 0 VOICE1 DECAY-ATTACK
  0 F VOICE1 RELEASE-SUSTAIN ;
: MORSE-TYPE ( W:ADR N -- )
  FOR DUPW 1+W SWAPW @ CW-EMIT NEXT
  DROPW ;
: MORSE-DEMO
  ENABLE-MORSE
  ENABLE-NOISE
  20 00 VOICE1 FREQUENCY
  8 TURN-UP ( 0F VOLUME )
  " CQ CQ DE SM0YSR K"
  COUNT MORSE-TYPE ;

