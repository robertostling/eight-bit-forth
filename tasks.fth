( MULTI-TASKING )
4 CONSTANT MAX-TASKS
( 2 BYTES PER TASK, STORING X AND S
  X = 0 INDICATES SLOT IS NOT USED )
CREATE TASKS MAX-TASKS 2* ALLOT
MAX-TASKS 1- 2* 0 ZTASK !
: ACTIVE-TASK [
  ZTASK LDYZP
  INY INY
  MAX-TASKS 2* CPYIMM
  2 BNE
  0 LDYIMM
  ZTASK STYZP
  TASKS LDA ADR,Y
  1 BEQ
  RTS ]
  ACTIVE-TASK ;
: FREE-TASK [
  ZTASK LDYZP
  INY INY
  MAX-TASKS 2* CPYIMM
  2 BNE
  0 LDYIMM
  ZTASK STYZP
  TASKS LDA ADR,Y
  1 BNE
  RTS ]
  FREE-TASK ;
: NEXT-TASK [
  CALL ACTIVE-TASK
  INY
  TASKS LDA ADR,Y  TAX TXS
  DEY
  TASKS LDA ADR,Y  TAX ] ;
: YIELD [
  ZTASK LDYZP
  TXA  TASKS STA ADR,Y
  INY
  TSX TXA  TASKS STA ADR,Y
  JUMP NEXT-TASK ] ;
: TERMINATE [
  ZTASK LDYZP
  0 LDA IMM
  TASKS STA ADR,Y
  INY
  TASKS STA ADR,Y
  JUMP NEXT-TASK ] ;
: LAUNCH ( W:WORD -- N ) >CODE [
  S>TW INX
  ZTASK LDYZP
  ZTEMP 2 + STXZP
  TXA  TASKS STA ADR,Y
  INY
  TSX TXA  TASKS STA ADR,Y
  CALL FREE-TASK
  ZTEMP 2 + LDXZP  TYA LSR IMPL A>TOS
  INY INY TYA
  ASL IMPL ASL IMPL ASL IMPL
  ASL IMPL ASL IMPL SEC 4 SBC IMM
  TAX
  TXS
  ' TERMINATE >CODE 1-W
  SWAP  LDA IMM PHA  LDA IMM PHA
  0 ZTEMP JMPI ] ;
: KILL ( N -- ) [
  >A  ASL IMPL  TAY  0 LDA IMM
  TASKS STA ADR,Y
  INY
  TASKS STA ADR,Y ] ;
: JIFFY@W 0 A1 @W SWAP ;
: JIFFYS 0 SWAP JIFFY@W +W
  BEGIN YIELD  JIFFY@W OVERW >=W UNTIL
  DROPW ;
: SECONDS FOR 3C JIFFYS NEXT ;
MAKE KEY
  BEGIN
    KERNAL-GETIN ?DUP
    SKIP YIELD
  AGAIN ;
MAKE EMIT KERNAL-EMIT YIELD ;

